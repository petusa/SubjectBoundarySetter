apply plugin: 'com.android.application'

android {
    compileSdkVersion 10
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.github.petusa.subjectboundarysetter"
        minSdkVersion 10
        targetSdkVersion 10
        versionCode "git -C ${projectDir} rev-list --first-parent --count HEAD".execute().text.trim().toInteger()
        versionName "git -C ${projectDir} describe --always --tags --dirty".execute().text.trim().replaceAll("^v", "")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def apkName = "SubjectBoundarySetter-${output.baseName}-${variant.versionName}.apk"
        output.outputFile = new File(output.outputFile.parent, apkName)
    }
}

dependencies {
    compile 'com.github.ma1co.OpenMemories-Framework:framework:-SNAPSHOT'
    provided 'com.github.ma1co.OpenMemories-Framework:stubs:-SNAPSHOT'
    compile 'com.nanohttpd:nanohttpd:2.1.1'
}
